extends Node
class_name TrackDatabase

signal database_updated

@onready var watcher = %DirectoryWatcher

var tracks: Dictionary = {}


class TrackData:
	var name: String
	var path: String
	var uuid: String


func get_track_by_uuid(uuid: String) -> TrackData:
	return tracks[uuid]


func _on_directory_watcher_content_changed():
	var new_tracks = {}
	var files = watcher.files.duplicate()
	files.sort()
	for file in files:
		var path = "%s/%s/%s.glb.import" % [watcher.directory, file, file]
		if not FileAccess.file_exists(path):
			continue
		path = path.trim_suffix(".import")
		var uuid = path
		var track = TrackData.new()
		track.name = file
		track.path = path
		track.uuid = uuid
		new_tracks[uuid] = track
	self.tracks = new_tracks
	self.database_updated.emit()
